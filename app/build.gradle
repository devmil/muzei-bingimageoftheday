apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        applicationId "de.devmil.muzei.bingimageofthedayartsource"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 10
        versionName "2.1"

        archivesBaseName = "muzei_biot-v${defaultConfig.versionName}-${buildTime()}"

        def bingImageOfTheDayAuthorityValue = "de.devmil.muzei.bingimageoftheday.provider.BingImageOfTheDayArtSource"
        manifestPlaceholders = [bingImageOfTheDayAuthority: bingImageOfTheDayAuthorityValue]
        buildConfigField "String", "BING_IMAGE_OF_THE_DAY_AUTHORITY", "\"${bingImageOfTheDayAuthorityValue}\""

        multiDexEnabled true

    }
    signingConfigs {
        release {
            def propertiesFile = file('../Key/keystore.properties')
            if(propertiesFile.exists()) {
                println "Keystore properties found! Building signed..."
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(propertiesFile))
                storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            } else {
                println "No Keystore properties found! Building unsigned..."
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false //EventBus seems not to work using the current config
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            def propertiesFile = file('../Key/keystore.properties')
            if(propertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    lint {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.0'
    implementation "androidx.core:core-ktx:$rootProject.ext.ktxVersion"
    implementation "androidx.work:work-runtime-ktx:$rootProject.ext.workManagerVersion"
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'de.greenrobot:eventbus:2.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.multidex:multidex:2.0.1'
}
repositories {
    mavenCentral()
}

static def buildTime() {
    return new Date().format("yyyyMMdd")
}

buildscript {
}
